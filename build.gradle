import groovy.json.JsonSlurper

buildscript {
    dependencies {
        classpath  'org.postgresql:postgresql:42.2.1'
    }
}

plugins {
    id 'org.liquibase.gradle' version '2.0.4'
    id "com.github.bjornvester.xjc" version "1.6.0"
    id 'java'
    id 'java-library'
    id 'application'
    id 'idea'
}

if (file('database.gradle').exists()) {
    apply from:'database.gradle'
}

def codeJson = new JsonSlurper().parseText(project.file("code.json").text)
// NOTE: codeJson.version is a String[], and any IDE warning here can be safely ignored
def jsonVersion = codeJson.version[0]
/* Get last version number from commit counts */
def lastTagHash = "git rev-list --tags --max-count=1".execute().text
def commitCount = "NO_TAG_PRESENT"
if (! lastTagHash.isEmpty()){
    lastTagHash = lastTagHash.substring(0, lastTagHash.length() - 1)
    commitCount = ("git rev-list "+lastTagHash+".. --count").execute().text
    commitCount = commitCount.substring(0, commitCount.length() - 1)
}
if (commitCount == "NO_TAG_PRESENT") {
    commitCount = "0"
}

def version = "$jsonVersion.$commitCount"
mainClassName = 'asl.seedscan.SeedScan'
applicationDefaultJvmArgs = ["-Xms64m", "-Xmx30g"]

repositories {
    mavenCentral()
}

dependencies {
    api project(':asl-java-utils')

    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3+'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3+'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3+'
    implementation group: 'com.sun.activation', name: 'javax.activation', version: '1.2.0'

    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.+'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.+'
    // may want to call jfreechart as an api in java utils to prevent redundant import?
    implementation group: 'org.jfree', name: 'jfreechart', version: '1.5.+'
    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.+'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.1'
    implementation group: 'com.mchange', name: 'c3p0', version: '0.9.5.2'
    implementation 'edu.sc.seis:seedCodec:1.0.11'
    implementation ('edu.sc.seis:TauP:2.5.0') {
        exclude group: 'org.slf4j'
    }

    testImplementation group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testImplementation group: 'junit', name: 'junit', version: '4.+'

    liquibaseRuntime 'org.liquibase:liquibase-core:3.8.1'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.1.1'
    liquibaseRuntime group: 'org.postgresql', name: 'postgresql', version: '42.2.1'

}


xjc {
    xsdDir.set(layout.projectDirectory.dir("src/main/resources/schemas"))
}


jar {
    /* Adds all dependent libraries*/
    from {
        configurations.compileClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    manifest {
        attributes 'Implementation-Title': 'ASL SeedScan Data Quality Analyzer',
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }
}

tasks.withType(Jar){
    setDuplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

test {
    maxHeapSize = "4096m"
    maxParallelForks = 4;
    testLogging {
        events "skipped", "failed"
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "Result: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

/*Turn off doclint since it is far too strict and breaks the javadoc everytime*/
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PRIVATE
}

if (file('database.gradle').exists()) {
    liquibase {
        activities {
            main {
                changeLogFile "src/main/resources/asl/seedscan/database/changelog/db.changelog-master.xml"
                url project.ext.mainUrl
                username project.ext.mainUsername
                password project.ext.mainPassword
            }
        }
        runList = "main"
    }
}

task gitSubmodule(type: Exec) {
    doFirst {
        description 'Update git submodules'
        println 'Downloading the test data submodule may take a long time, potentially hours depending on your connection speed.'
        println 'Interrupting may corrupt the test data submodule.'
        commandLine 'git', 'submodule', 'update', '--init', '--recursive'
    }
}

task stagingBuild(type: Exec, dependsOn: gitSubmodule) {
    doFirst {
        println 'Beginning STAGING build'
        println 'Checking out Staging version of submodules'
        commandLine 'sh', '-c', 'cd asl-java-utils; git fetch --all; git checkout origin/staging; cd ..;'
    }
    finalizedBy {
        build
    }
}

task standardBuild(dependsOn: gitSubmodule) {
    doFirst {
        println 'Beginning STANDARD build'
    }
    finalizedBy {
        build
    }
}

tasks.named('compileJava') {
    options.incremental = true
    options.compilerArgs << "-Xlint:deprecation"
    dependsOn ('xjc', ':asl-java-utils:jar')
}
